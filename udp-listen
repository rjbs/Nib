#!/bin/env perl
use v5.38.0;
use utf8;

binmode *STDOUT, ':encoding(UTF-8)';

use Getopt::Long::Descriptive;
use IO::Async::Socket;

use lib 'lib';
use Nib::TouchUtil;

my ($opt, $usage) = describe_options(
  '%c %o',
  [ 'port|p=i', 'port to listen on', { default => '9991' } ],
);

use Nib::Config;
use Nib::Dev;

my $config = Nib::Config->read_file('nib.toml');

my $dev = Nib::Dev->new(config => $config);
my $loop = IO::Async::Loop->new;
$loop->add($dev);

my $req = $dev->_do_http_request({
  family  => 'inet',
  path    => '/events?id=1,2,3,4', # 4 is touch
  headers => [
    Accept => '*/*',
    TouchEventsPort => $opt->port,
  ],
  on_error  => sub {
    die "ERROR: @_";
  },
  on_header => sub {
    my ($header) = @_;

    warn "REQUEST: " . $header->request->as_string . "\n";
    warn "HEADER: " . $header->as_string . "\n";

    # We return a chunk handler.
    return sub ($chunk) {
      warn "---BEGIN CHUNK---\n$chunk\n---END CHUNK---\n";
      return;
    };
  },
});

my $sock = IO::Async::Socket->new(
  on_recv => sub ($self, $dgram, $addr) {
    state $counter = 1;
    warn "<<";
    Nib::TouchUtil->dump_dgram($dgram, $counter++);
  },
);

$loop->add($sock);

$sock->bind(
  family   => 'inet',
  service  => $opt->port,
  socktype => 'dgram',
)->get;

say "Now listening on port " . $opt->port;

$loop->run;
